plugins {
    alias(libs.plugins.gradleutils)
    alias(libs.plugins.licenser).apply(false)
}

group = 'net.neoforged.trainingwheels'
gradleutils {
    version {
        branches.suffixBranch()
    }
    setupCentralPublishing()
}
version = gradleutils.version

changelog {
    from '1.0'
    disableAutomaticPublicationRegistration()
}

subprojects { proj ->
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'signing'
    apply plugin: libs.plugins.gradleutils.get().pluginId
    apply plugin: libs.plugins.licenser.get().pluginId
    apply plugin: 'maven-publish'

    group = 'net.neoforged.trainingwheels'
    version = rootProject.version
    gradleutils.setupSigning(signAllPublications: true)

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(8)
        withSourcesJar()
        withJavadocJar()
    }

    repositories {
        repositories {
            mavenCentral()
        }
    }

    license {
        header = rootProject.file('LICENSE-header')
        include '**/*.java'
        include '**/*.groovy'
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = "${proj.name}"
                    description = 'Library for testing Gradle Plugins both in isolation and in a full project.'
                    url = 'https://github.com/neoforged/trainingwheels'
                    scm {
                        url = 'https://github.com/neoforged/trainingwheels'
                        connection = 'scm:git:git://github.com/neoforged/trainingwheels.git'
                        developerConnection = 'scm:git:git@github.com:neoforged/trainingwheels.git'
                    }
                    issueManagement {
                        system = 'github'
                        url = 'https://github.com/neoforged/trainingwheels/issues'
                    }

                    licenses {
                        license {
                            name = 'LGPLv2.1'
                            url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'neoforged'
                            name = 'NeoForged'
                            email = 'contact@neoforged.net'
                            url = 'https://github.com/NeoForged/'
                        }
                    }
                }
            }
        }
        repositories {
            maven gradleutils.publishingMaven
        }
    }
}
