pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url = 'https://maven.neoforged.net/releases' }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.4.0'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            plugin('gradleutils', 'net.neoforged.gradleutils').version('5.0.3')
            plugin('licenser', 'net.neoforged.licenser').version('0.7.5')

            final mockito = version 'mockito', '4.11.0'
            final groovy = version 'groovy', '3.0.13'
            final junit = version 'junit', '5.9.2'
            final jetbrainsAnnotations = version 'jetbrains', '26.0.2-1'

            library('jimfs', 'com.google.jimfs:jimfs:1.2')
            library('guava', 'com.google.guava:guava:31.1-jre')

            library('commons-io', 'commons-io:commons-io:2.11.0')
            bundle('commons', ['commons-io'])

            library('jetbrains-annotations', 'org.jetbrains', 'annotations').versionRef(jetbrainsAnnotations)
            bundle('jetbrains', ['jetbrains-annotations'])

            library('mockito-core', 'org.mockito', 'mockito-core').versionRef(mockito)
            library('mockito-inline', 'org.mockito', 'mockito-inline').versionRef(mockito)
            bundle('mockito', ['mockito-core', 'mockito-inline'])

            library('groovy-core', 'org.codehaus.groovy', 'groovy').versionRef(groovy)
            bundle('groovy', ['groovy-core'])

            library('spock', 'org.spockframework:spock-core:2.1-groovy-3.0')
            library('junit', 'org.junit.jupiter', 'junit-jupiter').versionRef(junit)
        }
    }
}

include('base')
include('gradle-base')
include('gradle-functional')

rootProject.name = 'TrainingWheels'
